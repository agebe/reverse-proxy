plugins {
  id 'com.palantir.git-version' version '3.0.0'
  id 'maven-publish'
}

subprojects {

  apply plugin: 'java'
  apply plugin: 'java-library'
  apply plugin: 'eclipse'
  apply plugin: "maven-publish"
  apply plugin: 'signing'

  version = '0.1.0'
  group = 'io.github.agebe'

  repositories {
    mavenCentral()
  }

  compileJava {
    sourceCompatibility = '17'
    targetCompatibility = '17'
  }

  // https://docs.gradle.org/current/userguide/publishing_maven.html#publishing_maven:complete_example
  publishing {
    repositories {
      maven {
        def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
        def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
        url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        credentials {
          username = project.findProperty("sonatype.user") ?: ''
          password = project.findProperty("sonatype.password") ?: ''
        }
      }
    }
    publications {
      maven(MavenPublication) {
        from components.java
        pom {
          name = 'reverse-proxy'
          description = 'embeddable http reverse proxy'
          url = 'https://github.com/agebe/reverse-proxy'
          licenses {
            license {
              name = 'The Apache License, Version 2.0'
              url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }
          developers {
            developer {
              id = 'agebe'
              name = 'Andre Gebers'
              email = 'andre.gebers@gmail.com'
            }
          }
          scm {
            connection = 'scm:git:git://github.com/agebe/reverse-proxy.git'
            developerConnection = 'scm:git:ssh://github.com:agebe/reverse-proxy.git'
            url = 'https://github.com/agebe/reverse-proxy/tree/main'
          }
        }
      }
    }
  }

  signing {
    sign publishing.publications.maven
  }

  dependencies {
    implementation 'org.slf4j:slf4j-api:2.0.6'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.2'
  }

  java {
    withSourcesJar()
    withJavadocJar()
  }

  jar {
    manifest {
      attributes(
        'Implementation-Version': project.version,
        'Implementation-Title': project.name,
        'git-version': gitVersion())
    }
  }

  javadoc {
    options.links = ['https://docs.oracle.com/en/java/javase/17/docs/api/']
  }

  test {
    useJUnitPlatform()
  }

}
